grammar de.fhb.mdsd.Mobile with org.eclipse.xtext.common.Terminals

generate mobile "http://www.fhb.de/mdsd/Mobile"

Model:
	app=App
	(activities+=Activity)*;

App:
	'app' '{'
		'packageName' packageName=QualifiedName
		'design' design=('light' | 'dark' | 'lightWithDarkActionBar')
		('bottomBar' bottomBar=('true' | 'false'))?
	'}';

QualifiedName:
	ID ('.' ID)*;

Activity:
	'activity' (main='main')? name=ID '{'
		('label' label=STRING)?
		('navigation' '{'
			navigation=Navigation
		'}')?
		('menu' '{'
			menu=Menu
		'}')?
		('view' '{'
			view=View
		'}')?
	'}';

Navigation:
	{Navigation} elements+=(Tab | Row)*;
	
Menu:
	{Menu} items+=MenuItem*;
	
MenuItem:
	'item' '{'
		'title' title=STRING
		'showAsAction' showAsAction=('ifRoom' | 'never')
		('icon' icon=STRING)?
	'}';

Tab:
	'tab' (selected='selected')? '{'
		'text' text=STRING
		(frag=Fragment)?
	'}';

Row:
	'row' (selected='selected')? '{'
		'text' text=STRING
	'}';
	
Fragment:
	'fragment' '{'
		view=View
	'}';

View:
	{View} elements+=(Button | TextField | TextView)*;

Button:
	'button' '{'
		'text' text=STRING
	'}';
	
TextField:
	'textField' '{'
		'inputType' inputType=('date' | 'emailAddress' | 'multiLine' | 'number' | 'numberDecimal' | 'password' | 'personName' | 'postalAddress' | 'time')
		('hint' hint=STRING)?
	'}';
	
TextView:
	'textView' '{'
		'text' text=STRING
		'appearance' appearance=('large' | 'medium' | 'small')
	'}';