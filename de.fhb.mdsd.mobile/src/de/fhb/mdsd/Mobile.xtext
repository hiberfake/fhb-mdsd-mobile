grammar de.fhb.mdsd.Mobile with org.eclipse.xtext.common.Terminals

generate mobile "http://www.fhb.de/mdsd/Mobile"

Model:
	app=App
	(activities+=Activity)*
	(preferenceActivity=PreferenceActivity);

App:
	'app' '{'
		'packageName' packageName=QualifiedName
		'name' name=STRING
		'design' design=('light' | 'dark' | 'lightWithDarkActionBar')
		('bottomBar' bottomBar=('true' | 'false'))?
	'}';

QualifiedName:
	ID ('.' ID)*;

Activity:
	'activity' (main='main')? name=ID '{'
		('label' label=STRING)?
		('navigation' '{'
			navigation=Navigation
		'}')?
		('menu' '{'
			menu=Menu
		'}')?
		('view' '{'
			view=View
		'}')?
	'}';
	
PreferenceActivity:
	'preferences' '{'
		{PreferenceActivity} (categories+=PreferenceCategory)*
	'}';

PreferenceCategory:
	'preferenceCategory' title=STRING '{'
		(preferences+=Preference)*
	'}';
	
Preference:
	(CheckBoxPreference | EditTextPreference | ListPreference | SwitchPreference);
	
CheckBoxPreference:
	'checkBoxPreference' '{'
		'key' key=STRING
        'title' title=STRING
        ('icon' icon=STRING)?
		('defaultValue' defaultValue="true" | "false")?        
        ('summary' summary=STRING | 
        	('summaryOn' summaryOn=STRING
        	 'summaryOff' summaryOff=STRING))?
	'}';
	
EditTextPreference:
	'editTextPreference' '{'
		'key' key=STRING
        'title' title=STRING
        ('icon' icon=STRING)?       
        ('summary' summary=STRING)?
	'}';
	
ListPreference:
	'listPreference' (multi='multi')? '{'
		'key' key=STRING
        'title' title=STRING
        ('icon' icon=STRING)?       
        ('summary' summary=STRING)?
	'}';
	
SwitchPreference:
	'switchPreference' '{'
		'key' key=STRING
        'title' title=STRING
        ('icon' icon=STRING)?
        ('summary' summary=STRING | 
        	('summaryOn' summaryOn=STRING
        	 'summaryOff' summaryOff=STRING))?
	'}';

Navigation:
	{Navigation} elements+=(Tab | Row)*;
	
Menu:
	{Menu} menuItems+=MenuItem*;
	
MenuItem:
	'menuItem' (searchView='searchView' | refreshView='refreshView' | settings='settings')? '{'
		'id' id=STRING
		'title' title=STRING
		'showAsAction' showAsAction=('ifRoom' | 'never')
		('icon' icon=STRING)?
	'}';

Tab:
	'tab' (selected='selected')? '{'
		'text' text=STRING
		(frag=Fragment)?
	'}';

Row:
	'row' (selected='selected')? '{'
		'text' text=STRING
		(frag=Fragment)?
	'}';
	
Fragment:
	'fragment' '{'
		view=View
	'}';

View:
	{View} elements+=(Button | TextField | TextView)*;

Button:
	'button' '{'
		'text' text=STRING
	'}';
	
TextField:
	'textField' '{'
		'inputType' inputType=('date' | 'emailAddress' | 'multiLine' | 'number' | 'numberDecimal' | 'password' | 'personName' | 'postalAddress' | 'time')
		('hint' hint=STRING)?
	'}';
	
TextView:
	'textView' '{'
		'text' text=STRING
		'appearance' appearance=('large' | 'medium' | 'small')
	'}';